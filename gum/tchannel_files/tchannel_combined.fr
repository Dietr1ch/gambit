(* ****************************************************************** *)
(* *****                                                        ***** *)
(* *****  FeynRules model file: Simplified DM models            ***** *)
(* *****                                                        ***** *)
(* *****  Author: Benjamin Fuks--- Edited by Chris                            ***** *)
(* *****                                                        ***** *)
(* ****************************************************************** *)

(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)
M$ModelName = "DMsimp_t";

M$Information = { Authors->{"Benjamin Fuks"}, Emails->{"fuks@lpthe.jussieu.fr"},
                  Institutions->{"LPTHE / Sorbonne U."},
                  Date->"29.10.19", Version->"1.2",
                  URLs->"http://feynrules.irmp.ucl.ac.be/wiki/DMsimpt/"};

M$InteractionOrderLimit     = { {NP, 2} };
M$InteractionOrderHierarchy = { {QCD, 1}, {NP, 2}, {QED, 2} };

(* ************************** *)
(* *****     Fields     ***** *)
(* ************************** *)
M$ClassesDescription = {
  (* Dark matter *)
  
  S[100] == {
    ClassName -> Xs,
    SelfConjugate -> True,
    Mass -> {MXs, 11.},
    Width -> 0,
    PDG -> 99901
  },

  S[101] == {
    ClassName -> Xc,
    SelfConjugate -> False,
    Mass -> {MXc, 14.},
    QuantumNumbers -> {Q->0},
    Width -> 0,
    PDG -> 99902
  },
  
  F[100] == {
    ClassName -> Xm,
    SelfConjugate -> True,
    Mass -> {MXm, 10.},
    Width -> 0,
    PDG -> 99903
  },
  
  F[101] == {
    ClassName -> Xd,
    SelfConjugate -> False,
    Mass -> {MXd, 13.},
    QuantumNumbers -> {Q->0},
    Width -> 0,
    PDG -> 99904
  },
  
  V[100] == {
    ClassName -> Xv,
    SelfConjugate -> True,
    Mass -> {MXv, 12.},
    Width -> 0,
    PDG -> 99905
  },
  
  V[101] == {
    ClassName -> Xw,
    SelfConjugate -> False,
    QuantumNumbers -> {Q->0},
    Mass -> {MXw, 15.},
    Width -> 0,
    PDG -> 99906
  },

  (* Mediators *)
  
  S[203] == {
    ClassName -> YS,
    SelfConjugate -> False,
    Indices -> {Index[Colour]},
    QuantumNumbers -> {Y->2/3, Q->2/3},
    PDG -> 2000002,
    Mass  -> {MYS, 2000.},
    Width -> {WYS, 20.  }
  },
  
  F[203] == {
    ClassName -> YF,
    SelfConjugate -> False,
    Indices -> {Index[Colour]},
    QuantumNumbers -> {Y->2/3, Q->2/3},
    PDG -> 5920002,
    Mass  -> {MYF, 4000.},
    Width -> {WYF, 40.  }
  }

};

(* ************************** *)
(* *****   Parameters   ***** *)
(* ************************** *)
M$Parameters = {
       
 lamS == {
   ParameterType -> External,
   ComplexParameter -> False,
   TeX -> Subscript[\[Lambda],u],
   BlockName -> DMS3U,
   InteractionOrder -> {NP,1},
   Value -> 0.13
 },       
            
 lamF == {
   ParameterType -> External,
   ComplexParameter -> False,
   TeX -> Subscript[OverHat[\[Lambda]],u],
   BlockName -> DMF3U,
   InteractionOrder -> {NP,1},
   Value ->  0.23
 }
 
};

(* ************************** *)
(* *****   Lagrangian   ***** *)
(* ************************** *)

LDM := Block[{LKinDM, LKinF3, LIntF3, ii,cc,mu,nu,sp,sp2},
  LKinDM :=  \
      1/2 del[Xs,mu]^2 - 1/2 MXs^2 Xs^2 \
      + del[Xc,mu] del[Xcbar,mu] - MXc^2 Xc Xcbar \
      + I/2*Xmbar.Ga[mu].DC[Xm,mu] - 1/2 MXm Xmbar.Xm \
      + I*Xdbar.Ga[mu].DC[Xd,mu] - MXd Xdbar.Xd \
      - 1/4 FS[Xv,mu,nu]^2 + 1/2 MXv^2 Xv[mu]^2 \
      - 1/2 FS[Xw,mu,nu] FS[Xwbar,mu,nu] + MXw^2 Xw[mu] Xwbar[mu];

  LKinS3 :=  \
      Plus @@ (DC[anti[#],mu] DC[#,mu] &/@ { YSbar[cc]}) \
    - MYS^2  YSbar[cc]  YS[cc];

  LKinF3 :=  \
      Plus @@ (I anti[#].Ga[mu].DC[#,mu] &/@ {YF} ) \
    - MYF YFbar[sp,cc].YF[sp,cc];

  LIntS3 := \
   + lamS Xmbar[sp].uR[sp,1,cc] YSbar[cc] \
   + lamS Xdbar[sp].uR[sp,1,cc] YSbar[cc];

  LIntF3 := \
   + lamF YFbar[sp,cc].uR[sp2,1,cc] (Xs IndexDelta[sp,sp2]) \
   + lamF YFbar[sp,cc].uR[sp2,1,cc] (Xc IndexDelta[sp,sp2]) \
   + lamF YFbar[sp,cc].uR[sp2,1,cc] (Xv[mu] Ga[mu,sp,sp2]) \
   + lamF YFbar[sp,cc].uR[sp2,1,cc] (Xw[mu] Ga[mu,sp,sp2]);

LKinDM + LKinS3 + LIntS3 + HC[LIntS3] + LKinF3 + LIntF3 + HC[LIntF3]];



